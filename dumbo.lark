program: (TXT|dumbo_block)+

?dumbo_block: "{{" expressions_list "}}"

expressions_list: (expression ";")+

expression: "print" string_expression -> print_statement
          | "for" VARIABLE "in" (string_list|VARIABLE) "do" expressions_list "endfor" -> for_statement
          | "if" boolean_expression "do" expressions_list "endif" -> if_statement
          | VARIABLE ":=" (string_expression|string_list) -> assign

?arithmetic_expression: arithmetic_expression ADD_OP arithmetic_expression
                      | arithmetic_expression MULL_OP arithmetic_expression
                      | (INT|VARIABLE)

?string_expression: string_expression "." string_expression
                  | (STRING | arithmetic_expression | boolean_expression)

boolean_expression: boolean_expression LOG_OP boolean_expression
                  | arithmetic_expression COMP_OP arithmetic_expression
                  | "true" -> true
                  | "false" -> false

string_list: "(" STRING ("," STRING)* ")"

ADD_OP: "+" | "-"
MULL_OP: "*" | "/"
LOG_OP: "and" | "or"
COMP_OP: "<" | ">" | "=" | "!="
TXT: /((?!{{).)+/
STRING: "'" _STRING_ESC_INNER "'"

%import common._STRING_ESC_INNER
%import common.CNAME -> VARIABLE
%import common.INT
%import common.WS
%ignore WS